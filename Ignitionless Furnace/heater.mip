    alias PumpIn d0
    alias PumpOut d1
    alias Analyzer d2
    alias Moles d3
    alias Temperature d4

    alias molesTarget r6
    alias molesCurrent r7
    alias molesLower r8
    alias molesUpper r9
    alias tempTarget r10
    alias tempCurrent r11
    alias tempLower r12


    define molesHysteresis 10
    define tempHysteresis 10
    define pumpSetting 100

    define maxPressure 50000                        # switch off heaters when this pressure is exceeded

    define hashHeater -419758574

init:
    s PumpIn On 0
    s PumpIn Setting pumpSetting
    s PumpOut On 0
    s PumpOut Setting pumpSetting
    s Analyzer On 1

loop:
    jal calcParams
    jal adjustMoles
    jal adjustTemperature

    s db Setting molesCurrent

done:
    yield
    j loop


calcParams:
    l molesTarget Moles Setting
    l molesCurrent Analyzer TotalMoles
    sub molesLower molesTarget molesHysteresis
    add molesUpper molesTarget molesHysteresis

    l tempTarget Temperature Setting
    l tempCurrent Analyzer Temperature
    sub tempLower tempTarget tempHysteresis

    j ra

adjustMoles:
    brgt molesCurrent molesLower 4
    s PumpIn On 1
    s PumpOut On 0
    j ra

    brgt molesCurrent molesTarget 3
    s PumpOut On 0
    j ra

    brgt molesCurrent molesUpper 3
    s PumpIn On 0
    j ra

    s PumpIn On 0
    s PumpOut On 1

    j ra

adjustTemperature:
    l r0 Analyzer Pressure
    ble r0 maxPressure pressureIsSafe
    sb hashHeater On 0                  # turn heaters off above max pressure
    j ra

pressureIsSafe:
    brgt tempCurrent tempLower 3
    sb hashHeater On 1
    j ra

    brgt tempCurrent tempTarget 2
    j ra

    sb hashHeater On 0

    j ra
