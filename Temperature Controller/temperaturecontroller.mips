# Controller for a number of heaters and coolers
#
# Written by Zappes
# CC BY 4.0 (https://creativecommons.org/licenses/by/4.0/)

define defaultTarget 20     # Default value: target temperature
define defaultHysteresis 5  # Default value: hysteresis

alias Sensor d0
alias Display d1
alias dialTarget d2         # optional dial for target temperature
alias dialHysteresis d3     # optional dial for hysteresis

alias temperature r10
alias coolAbove r11
alias heatUnder r12
alias target r13
alias hysteresis r14

define hashCooler -739292323
define hashHeater 24258244
define colCold 0
define colGood 2
define colHot 4

loop:
    jal readDials           # read dials for hysteresis and target, if present
    jal updateThresholds    # set up heatUnder/coolAbove
    jal updateTemperature   # read sensor & convert to celsius
    jal updateDisplay       # display temperature, if a display is connected
    jal checkHeaters        # heaters on/off
    jal checkCoolers        # coolers on/off
    yield                   # leave some CPU cycles for the actual game...
    j loop                  # here we go again

checkCoolers:
    move r0 temperature
    ble r0 target ccOff     # disable cooler if temperature <= target
    bge r0 coolAbove ccOn   # enable cooler if temperature >= target+hysteresis
    j ccDone

ccOff:
    move r1 0
    sb hashCooler On r1
    j chDone

ccOn:
    move r1 1
    sb hashCooler On r1

ccDone:
    j ra

checkHeaters:
    move r0 temperature
    bge r0 target chOff     # disable heater if temperature >= target
    ble r0 heatUnder chOn   # enable heater if temperature <= target-hysteresis
    j chDone

chOff:
    move r1 0
    sb hashHeater On r1
    j chDone

chOn:
    move r1 1
    sb hashHeater On r1

chDone:
    j ra


updateTemperature:
    l temperature Sensor Temperature
    sub temperature temperature 273.15
    round temperature temperature
    j ra

updateDisplay:
    bdns Display udDone                     # ignore if no display is connected
    s Display Setting temperature

    brge temperature heatUnder 3
    s Display Color colCold
    j udDone
    brge temperature coolAbove 3
    s Display Color colGood
    j udDone
    s Display Color colHot

udDone:
    j ra

updateThresholds:
    # set up the thresholds at which the heaters and coolers kick in
    move coolAbove target
    add coolAbove coolAbove hysteresis
    move heatUnder target
    sub heatUnder heatUnder hysteresis
    j ra

readDials:
    bdns dialTarget rdNoTarget
    l target dialTarget Setting
    j rdCheckHysteresis

rdNoTarget:
    move target defaultTarget

rdCheckHysteresis:
    bdns dialHysteresis rdNoHysteresis
    l hysteresis dialHysteresis Setting
    j rdDone

rdNoHysteresis:
    move hysteresis defaultHysteresis

rdDone:
    j ra